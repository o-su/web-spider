buildscript {
  ext.kotlin_version = "1.3.72"
  ext.jsoup_version = "1.10.3"

  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply plugin: "kotlin"
apply plugin: "application"

mainClassName = "WebSpider.WebSpiderKt"

defaultTasks "run"

repositories {
  mavenCentral()
}

configurations {
  ktlint
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile "org.jsoup:jsoup:$jsoup_version"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7"
  testCompile "junit:junit:4.11"
  testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
  ktlint "com.pinterest:ktlint:0.37.2"
}

jar {
  manifest { attributes "Main-Class": "WebSpider.WebSpiderKt" }
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task wrapper(type: Wrapper) {
  gradleVersion = "4.10.2"
}

task ktlint(type: JavaExec, group: "verification") {
  description = "Check Kotlin code style."
  main = "com.pinterest.ktlint.Main"
  classpath = configurations.ktlint
  args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
  description = "Fix Kotlin code style deviations."
  main = "com.pinterest.ktlint.Main"
  classpath = configurations.ktlint
  args "-F", "src/**/*.kt"
}
